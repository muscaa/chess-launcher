ext.subName = "core"

configurations {
    include {
        canBeResolved = true
        canBeConsumed = false
    }
    
    runtimeClasspath.extendsFrom(include)
    compileClasspath.extendsFrom(include)
}

dependencies {
	implementation project(":bootstrap")
	
	include "com.formdev:flatlaf:3.5.4"
	include "com.github.rjeschke:txtmark:0.13"
}

tasks.register("bundle", Zip) {
    destinationDirectory.set(file("${buildDir}/libs"))
    archiveFileName.set("${subName}-bundle.zip")
	
    from(tasks.named("jar")) {
        into("libs")
    }
	
    from(configurations.include) {
        into("libs")
    }
}

tasks.named("jar") {
    finalizedBy("bundle")
}

java {
	withSourcesJar()
	withJavadocJar()
}

publishing {
    publications {
        maven(MavenPublication) {
			from components.java
			
			artifact(tasks.named("bundle")) {
                classifier = "bundle"
            }
        }
    }
}
